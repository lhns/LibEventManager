apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'eclipse'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
}

jar {
    manifest {
        if (project.hasProperty("mainClass")) attributes 'Main-Class': mainClass
    }
}

task testJar(type: Jar, dependsOn: javadoc) {
    from sourceSets.main.output
    from sourceSets.test.output

    manifest {
        if (project.hasProperty("testMainClass")) attributes "Main-Class": testMainClass
    }
    classifier = 'test'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier = 'javadoc'
}

build {
    dependsOn testJar
    dependsOn sourceJar
    dependsOn javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact testJar {
                classifier 'test'
            }

            artifact sourceJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }

    // Use -PmavenUsername and -PmavenPassword
    repositories.maven {
        if (!(project.hasProperty("mavenUrl") &&
                project.hasProperty("mavenUsername") &&
                project.hasProperty("mavenPassword")))
            return;

        credentials {
            username mavenUsername
            password mavenPassword
        }
        url mavenUrl
    }
}